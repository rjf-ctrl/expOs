// Module 1 - PROCESS_MANAGERs

alias functionNum R1;
alias PID R2;

// 1 = Free User Area Page
// 2 = Free Page Table  
// 3 = Exit Process


if(functionNum == 1) then

    alias userAreaPage R3;
    userAreaPage = [PROCESS_TABLE + (PID * 16) + 11];
    
    //Release Page (MOD_2, function 2)
    multipush(R1, R2);
    R1 = 2;         // RELEASE_PAGE function number
    R2 = userAreaPage;
    call MOD_2;
    multipop(R1, R2);
    
    return;
endif;


if(functionNum == 2) then

    alias ptbr R3;
    alias i R4;
    
    ptbr = [PROCESS_TABLE + (PID * 16) + 14];
    
    //release library pages
    [ptbr + 0] = -1;
    [ptbr + 1] = "0000";
    [ptbr + 2] = -1;
    [ptbr + 3] = "0000";
    
    
    i = 4;  
    while(i < 20) do
        if([ptbr + i] != -1) then
            //release the page
            multipush(R1, R2, R3, R4);
            R1 = 2;             // RELEASE_PAGE 
            R2 = [ptbr + i];   
            call MOD_2;
            multipop(R1, R2, R3, R4);
            
            //reset the entry
            [ptbr + i] = -1;
            [ptbr + i + 1] = "0000";
        endif;
        i = i + 2;  
    endwhile;
    
    return;
endif;


if(functionNum == 3) then

    // Free Page Table
    multipush(R1, R2);
    R1 = 2;    
    R2 = PID;
    call MOD_1;
    multipop(R1, R2);
    
    // Free User Area Page
    multipush(R1, R2);
    R1 = 1;     
    R2 = PID;
    call MOD_1;
    multipop(R1, R2);
    
    // Set process state to TERMINATED
    [PROCESS_TABLE + (PID * 16) + 4] = TERMINATED;
    
    return;
endif;